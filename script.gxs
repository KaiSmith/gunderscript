/**
 * Gunderscript Example Script
 * (C) 2014 Christian Gunderman
 */

/**
 * A recursive factorial example.
 */
function fact(n) {
  if(n == 1) {
    return (1);
  }

  return (n * fact(n - 1));
}

/**
 * Entry point to the script. Exported keyword tells the compiler to remember
 * the location in byte code of this function for use as an entry point.
 */
function exported main() {

  /* variable declaration. declaration and assignment in the same line does not
   * work yet (var x = 1;). At the moment, you are confined to declaring and 
   * initializing separately. Variables must be declared at top of method body,
   * just like in C89.
   */
  var x;
  var i;

  // demonstrates precedence of operators and assignment statements:
  x = 1;
  x = 4 * 2 + x + 2;

  /* demonstrates if statements and boolean logic
   */
   if(true) {
     if(true) {
       print("Look! A nested if statement. ");
     }

     print("Here we did some " + "math operations: ", 1 + 2 * fact(x) + 1);
     print(" Boolean Comparison: ", (3 + 3) > (2 + 2) || 5 <= 7);
   } else {
     print(false);
   }
   
   /* count down from ten, a loop example */
   i = 10;
   while(i != 0) {
     print(i, ", ");
     i = i - 1;
   }
}